//
// Example Program: Thresholding
//

apron.config_begin
    sim.pix_source_webcam
    host.template('../scamp5c_host_template.txt')
    host.firmware('../ok_s5c_ipu.bit')
    host.set_frame_rate(25)
    window_1 = host.add_display('Capture')
    window_2 = host.add_display('Result')
    // create a slider in the host UI to allow the threshold value to be changed dynamically
    slider_1 = host.add_slider('threshold', -120, 120, 15, 1, 1)
apron.config_end

#frame_loop

    // use double frame trigger to maintain the exposure time
    ipu.wait_frame_trigger
    rpix
    
    ipu.wait_frame_trigger
    rpix(B,C)
    
    // output the original image
    usb.aout(C,window_1)
    
    // fill A with -128 (darkest level)
    A = in(-128)
    
    // get the threshold value from the slider in the host UI
    s0 = usb.slider(slider_1)
    
    // calculate: E = C - D, after D is filled with our threshold value
    D = in(s0)
    E = sub(C,D)
    
    // flag those PEs where E > 0 (essentially achieved where C > D)
    where(E)
        // only those flagged PE will do: 
        // fill A with 127 (brightest level)
        A = in(127)
    all
    
    // output the result image
    usb.aout(A,window_2)
    
    _jump(#frame_loop)
    
apron.genereate_host
apron.compile_host
